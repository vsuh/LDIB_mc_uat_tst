#!/progra~2/OneScript/bin/oscript.exe -encoding=utf8
// Вызов:                                                                                             VSCraft@2022
// > oscript path\full_rights.os ИБ [0/1]
// где:
//      ИБ - имя ИБ из файла ibs.json
// второй параметр переопределяет переменную `УбратьПолныеПрава` "0" - Истина, любое другое - Ложь
Перем имяФайлаПодрядчиков; // имя файла с параметрами
Перем УбратьПолныеПрава;   // флаг: установить или убрать "ПолныеПрава" у пользователей

Функция ПрочитатьПараметры(Отказ)
	Если АргументыКоманднойСтроки.Количество() = 0 Тогда
		Сообщить("### Требуется параметр в строке вызова");
	Иначе
		ИмяИБ = АргументыКоманднойСтроки[0];
	КонецЕсли;
	Если АргументыКоманднойСтроки.Количество() = 2 Тогда
		Если АргументыКоманднойСтроки[1] = "0" Тогда
			УбратьПолныеПрава = Истина;
		Иначе
			УбратьПолныеПрава = Ложь;
		КонецЕсли;
	КонецЕсли;

	ИмяАдминистратораИБ  = ПолучитьПеременнуюСреды("ibADMIN"); 
	ПарольАдминистратора = ПолучитьПеременнуюСреды("ibPASSWORD"); 
	Если ПустаяСтрока("" + ИмяАдминистратораИБ + ПарольАдминистратора) Тогда
		Message("### Не определены переменные среды ""ibADMIN"" или ""ibPASSWORD""");
	КонецЕсли;

	СтруктураПараметров = ПолучитьПараметрыИБ(ИмяИБ, Отказ);
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;

	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Сервер", СтруктураПараметров["Сервер"]);
	СтруктураРезультат.Вставить("ИБ", СтруктураПараметров["ИБ"]);
	СтруктураРезультат.Вставить("Имя", ИмяАдминистратораИБ);
	СтруктураРезультат.Вставить("Пароль", ПарольАдминистратора);
	СтруктураРезультат.Вставить("Подрядчики", СтруктураПараметров["users"]);
	
	Возврат СтруктураРезультат;
КонецФункции

Функция ПолучитьПараметрыИБ(ИмяИБ, Отказ)
	Если ПустаяСтрока(имяФайлаПодрядчиков) Тогда
		Message("### Не определено имя файла параметров");
	КонецЕсли;
	ФайлПроверки = Новый Файл(имяФайлаПодрядчиков);
	Если Не ФайлПроверки.Существует() Тогда
		Message("### Не найден файл параметров " + ФайлПроверки.ПолноеИмя);
	КонецЕсли;
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;

	ЧтениеДж = Новый ЧтениеJSON;
	ЧтениеДж.ОткрытьФайл(имяФайлаПодрядчиков);
		
	Попытка
		ПараметрыИзФайлаНастроек = ПрочитатьJSON(ЧтениеДж, Истина);
		СтруктураНастроекИБ = ПараметрыИзФайлаНастроек["ibs"][ИмяИБ];
	Исключение
		ЧтениеДж.Закрыть();
		Message("### Ошибка десериализации данных из файла " + имяФайлаПодрядчиков);
	КонецПопытки;

	ЧтениеДж.Закрыть();
	Если СтруктураНастроекИБ = Неопределено 
		Или СтруктураНастроекИБ["users"].Количество() = 0 Тогда
		Сообщить("Пустой список подрядчиков");
		Отказ = Истина;
		Возврат Неопределено;
	КонецЕсли;

	Возврат СтруктураНастроекИБ;
КонецФункции
Процедура ПредоставитьПолныеПрава(СтруктураПараметров, Отказ)
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Если СтруктураПараметров = Неопределено Тогда
		Message("### Параметры не получены");
	КонецЕсли;
	текИБ = ПолучитьСоединение(СтруктураПараметров);

	текИБ.УстановитьПривилегированныйРежим(Истина);
	Для Каждого ИмяПодрядчика Из СтруктураПараметров.Подрядчики Цикл
		ПользовательБД = текИБ.ПользователиИнформационнойБазы.НайтиПоИмени(ИмяПодрядчика);
		Если Неопределено = ПользовательБД Тогда
			Отказ = Истина;
			Сообщить("" + ИмяПодрядчика + " не найден");
			Продолжить;
		Иначе 
			Если УбратьПолныеПрава Тогда
				Если ПользовательБД.Роли.Содержит(текИБ.Метаданные.Роли.ПолныеПрава) Тогда
					ПользовательБД.Роли.Удалить(текИБ.Метаданные.Роли.ПолныеПрава);
					ПользовательБД.Записать();
				КонецЕсли;
				Иначе
				Если НЕ ПользовательБД.Роли.Содержит(текИБ.Метаданные.Роли.ПолныеПрава) Тогда
					ПользовательБД.Роли.Добавить(текИБ.Метаданные.Роли.ПолныеПрава);
					ПользовательБД.Записать();
				КонецЕсли;
			КонецЕсли;
			Сообщить("+ " + ПользовательБД.Имя);
		КонецЕсли;
	КонецЦикла;
	текИБ.УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

Функция ПолучитьСоединение(СтруктураПараметров)
	КО = Новый COMObject("V83.COMConnector");
	СтрокаСоединения = "Srvr=""" + СтруктураПараметров.Сервер + """;Ref=""" + СтруктураПараметров.ИБ + """;Usr="""+СтруктураПараметров.Имя+""";Pwd="""+СтруктураПараметров.Пароль+""";";
	Сообщить("COM соединение с ИБ " + СтруктураПараметров.ИБ + "....");
	connection = КО.Connect(СтрокаСоединения);
	Возврат connection;
КонецФункции
//************************************************************//
имяФайлаПодрядчиков = ОбъединитьПути(ТекущийСценарий().Каталог, "ibs.json");
УбратьПолныеПрава = Ложь;
Отказ = Ложь;
Параметры = ПрочитатьПараметры(Отказ);
ПредоставитьПолныеПрава(Параметры, Отказ);
Если Отказ Тогда
	Сообщить("Неуспешное завершение скрипта");
КонецЕсли;
ЗавершитьРаботу(0);
